/*
-----------------------------------------------------------------------------
This source file is part of OGRE
(Object-oriented Graphics Rendering Engine)
For the latest info, see http://www.ogre3d.org

Copyright (c) 2000-2014 Torus Knot Software Ltd
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
-----------------------------------------------------------------------------
*/

//-----------------------------------------------------------------------------
// Program Name: SGXLib_Lighting
// Program Desc: Per pixel lighting functions.
// Program Type: Vertex/Pixel shader
// Language: CG
//-----------------------------------------------------------------------------
#define M_PI 3.141592654

//-----------------------------------------------------------------------------
void SGX_FetchNormal(in sampler2D s,
				   in float2 uv,
				   out float3 vOut)
{
	vOut = 2 * tex2D(s, uv).xyz - 1;
}

//-----------------------------------------------------------------------------
void SGX_Light_Directional_Diffuse(
				   in float3 vNormal,
				   in float3 vNegLightDirView,
				   in float3 vDiffuseColour,
				   inout float3 vOut)
{
	float3 vNormalView = normalize(vNormal);
	float nDotL = dot(vNormalView, vNegLightDirView);
	
	vOut += vDiffuseColour * saturate(nDotL);
	vOut = clamp(vOut, 0.0, 1.0);
}

//-----------------------------------------------------------------------------
void SGX_Light_Directional_DiffuseSpecular(
					in float3 vNormal,
					in float3 vViewDir,
					in float3 vNegLightDirView,
					in float3 vDiffuseColour, 
					in float3 vSpecularColour, 
					in float fSpecularPower,
					inout float3 vOutDiffuse,
					inout float3 vOutSpecular)
{
	float3 vNormalView = normalize(vNormal);		
	float nDotL		   = dot(vNormalView, vNegLightDirView);			
	float3 vView       = normalize(vViewDir);
	float3 vHalfWay    = normalize(vView + vNegLightDirView);
	float nDotH        = dot(vNormalView, vHalfWay);
	
	if (nDotL > 0)
	{
		vOutDiffuse  += vDiffuseColour * nDotL;		
#ifdef NORMALISED
		vSpecularColour *= (fSpecularPower + 8.0)/(8.0 * M_PI);
#endif
		vOutSpecular += vSpecularColour * pow(saturate(nDotH), fSpecularPower);
        vOutDiffuse = clamp(vOutDiffuse, 0.0, 1.0);
        vOutSpecular = clamp(vOutSpecular, 0.0, 1.0);
	}
}

//-----------------------------------------------------------------------------
void SGX_Light_Point_Diffuse(
				    in float3 vNormal,
				    in float3 vLightView,
				    in float4 vAttParams,
				    in float3 vDiffuseColour, 
				    inout float3 vOut)
{
	float fLightD      = length(vLightView);
	float3 vNormalView = normalize(vNormal);
	float nDotL        = dot(vNormalView, normalize(vLightView));
	
	if (nDotL > 0 && fLightD <= vAttParams.x)
	{
		float fAtten	   = 1 / (vAttParams.y + vAttParams.z*fLightD + vAttParams.w*fLightD*fLightD);
			
		vOut += vDiffuseColour * nDotL * fAtten;
        vOut = clamp(vOut, 0.0, 1.0);
	}
}



//-----------------------------------------------------------------------------
void SGX_Light_Point_DiffuseSpecular(
				    in float3 vNormal,
				    in float3 vViewDir,
				    in float3 vLightView,
				    in float4 vAttParams,
				    in float3 vDiffuseColour, 
				    in float3 vSpecularColour, 
					in float fSpecularPower,
					inout float3 vOutDiffuse,
					inout float3 vOutSpecular)
{
	float fLightD      = length(vLightView);
	vLightView		   = normalize(vLightView);	
	float3 vNormalView = normalize(vNormal);
	float nDotL        = dot(vNormalView, vLightView);	
		
	if (nDotL > 0 && fLightD <= vAttParams.x)
	{					
		float3 vView       = normalize(vViewDir);
		float3 vHalfWay    = normalize(vView + vLightView);		
		float nDotH        = dot(vNormalView, vHalfWay);
		float fAtten	   = 1 / (vAttParams.y + vAttParams.z*fLightD + vAttParams.w*fLightD*fLightD);					
		
		vOutDiffuse  += vDiffuseColour * nDotL * fAtten;
#ifdef NORMALISED
		vSpecularColour *= (fSpecularPower + 8.0)/(8.0 * M_PI);
#endif
		vOutSpecular += vSpecularColour * pow(saturate(nDotH), fSpecularPower) * fAtten;
        vOutDiffuse = clamp(vOutDiffuse, 0.0, 1.0);
	    vOutSpecular = clamp(vOutSpecular, 0.0, 1.0);
	}
}

//-----------------------------------------------------------------------------
void SGX_Light_Spot_Diffuse(
				    in float3 vNormal,
				    in float3 vLightView,
				    in float3 vNegLightDirView,
				    in float4 vAttParams,
				    in float3 vSpotParams,
				    in float3 vDiffuseColour, 
				    inout float3 vOut)
{
	float fLightD      = length(vLightView);
	vLightView		   = normalize(vLightView);
	float3 vNormalView = normalize(vNormal);
	float nDotL        = dot(vNormalView, vLightView);
	
	if (nDotL > 0 && fLightD <= vAttParams.x)
	{
		float fAtten	= 1 / (vAttParams.y + vAttParams.z*fLightD + vAttParams.w*fLightD*fLightD);
		float rho		= dot(vNegLightDirView, vLightView);						
		float fSpotE	= saturate((rho - vSpotParams.y) / (vSpotParams.x - vSpotParams.y));
		float fSpotT	= pow(fSpotE, vSpotParams.z);	
						
		vOut += vDiffuseColour * nDotL * fAtten * fSpotT;
        vOut = clamp(vOut, 0.0, 1.0);
	}
}

//-----------------------------------------------------------------------------
void SGX_Light_Spot_DiffuseSpecular(
				    in float3 vNormal,
				    in float3 vViewDir,
				    in float3 vLightView,
				    in float3 vNegLightDirView,
				    in float4 vAttParams,
				    in float3 vSpotParams,
				    in float3 vDiffuseColour, 
				    in float3 vSpecularColour, 
					in float fSpecularPower,
					inout float3 vOutDiffuse,
					inout float3 vOutSpecular)
{
	float fLightD      = length(vLightView);
	vLightView		   = normalize(vLightView);
	float3 vNormalView = normalize(vNormal);
	float nDotL        = dot(vNormalView, vLightView);
	
	
	if (nDotL > 0 && fLightD <= vAttParams.x)
	{
		float3 vView       = normalize(vViewDir);
		float3 vHalfWay    = normalize(vView + vLightView);				
		float nDotH        = dot(vNormalView, vHalfWay);
		float fAtten	= 1 / (vAttParams.y + vAttParams.z*fLightD + vAttParams.w*fLightD*fLightD);
		float rho		= dot(vNegLightDirView, vLightView);						
		float fSpotE	= saturate((rho - vSpotParams.y) / (vSpotParams.x - vSpotParams.y));
		float fSpotT	= pow(fSpotE, vSpotParams.z);	
						
		vOutDiffuse  += vDiffuseColour * nDotL * fAtten * fSpotT;
#ifdef NORMALISED
		vSpecularColour *= (fSpecularPower + 8.0)/(8.0 * M_PI);
#endif
		vOutSpecular += vSpecularColour * pow(saturate(nDotH), fSpecularPower) * fAtten * fSpotT;
        vOutDiffuse = clamp(vOutDiffuse, 0.0, 1.0);
	    vOutSpecular = clamp(vOutSpecular, 0.0, 1.0);
	}
}
